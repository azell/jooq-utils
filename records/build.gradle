plugins {
  id("java-library")
  id("nu.studer.jooq")
}

dependencies {
  jooqRuntime(Libs.logbackClassic)
  jooqRuntime(Libs.postgresql)
  jooqRuntime(project(":generators"))

  annotationProcessor(Libs.immutablesProcessor)
  api(Libs.guava, "org.jooq:jooq")
  compileOnly(Libs.javaAnnotations)
  compileOnly(Libs.immutablesAnnotations, Libs.immutablesBuilder)
}

jooq {
  version = Versions.jooq
  sample(sourceSets.main) {
    jdbc {
      driver = "org.postgresql.Driver"
      url = "jdbc:postgresql://localhost:5432/sampledb"
      user = "postgres"
      password = "mysecretpassword"
    }
    generator {
      name = "com.github.azell.jooq_utils.generators.BuilderGenerator"
      database {
        name = "org.jooq.meta.postgres.PostgresDatabase"
        includes = ".*"
        excludes = "information_schema[.].*|databasechangelog.*"
        inputSchema = "public"
      }
      generate {
        deprecated = false
        immutablePojos = true
        interfaces = true
        javaTimeTypes = true
        pojos = true
        pojosEqualsAndHashCode = true
        serializablePojos = false
      }
      target {
        packageName = "com.github.azell.jooq_utils.sample.data"
      }
    }
  }
}

tasks {
  compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }
}

generateSampleJooqSchemaSource.dependsOn(":schema:update")
